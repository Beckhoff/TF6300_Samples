<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.19">
  <POU Name="MAIN" Id="{fc7ad009-dba4-43b5-8d0c-68fc688de40d}">
    <Declaration><![CDATA[PROGRAM MAIN
VAR
	RisingEdge 			: R_TRIG;
	startstop 			: BOOL;
	state				: BYTE;
	FB_FTP_Open1 		: FB_FTP_Open;
	FB_FTP_FileDownload1: FB_FTP_FileDownload;
	FB_FTP_Close1 		: FB_FTP_Close;
	busy 				: BOOL;
	err 				: BOOL;
	errid 				: UDINT;
	handle 				: T_HFTP;
	progress 			: UDINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[RisingEdge(CLK:= startstop);
IF RisingEdge.Q THEN
	state := 1;
END_IF

CASE state OF
	0:
		;

	1:
		FB_FTP_Open1(
			sNetID:= ,
			sHost:= '172.17.216.47',
			nPort:= 21,
			sUsername:= 'TestUser',
			sPassword:= 'TestPwd123',
			bExecute:= TRUE,
			tTimeout:= T#15s,
			bBusy=> busy,
			bError=> err,
			nErrId=> errid,
			hFTP=> handle);

		IF NOT busy AND NOT err THEN
			FB_FTP_Open1(bExecute:= FALSE);
			state := 2;
		END_IF

	2:
		FB_FTP_FileDownload1(
			sNetID:= ,
			hFTP:= handle,
			sSrcFile:= '\TestFolder\File1.txt',
			sDesFile:= '\Program Files\TestFile1.txt',
			bExecute:= TRUE,
			tTimeout:= T#15s,
			bBusy => busy,
			bError => err,
			nErrId => errid,
			nProgress => progress);

		IF NOT busy AND NOT err THEN
			FB_FTP_FileDownload1(bExecute:= FALSE);
			state := 3;
		END_IF

	3:
		FB_FTP_Close1(
			sNetID:= ,
			hFTP:= handle,
			bExecute:= TRUE,
			tTimeout:= T#15s,
			bBusy => busy,
			bError => err,
			nErrId => errid);

		IF NOT busy AND NOT err THEN
			FB_FTP_Close1(bExecute:= FALSE);
			state := 0;
		END_IF

END_CASE]]></ST>
    </Implementation>
    <ObjectProperties />
  </POU>
</TcPlcObject>